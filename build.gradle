plugins {
    id "java"
    id "org.asciidoctor.jvm.convert"
    id "org.springframework.boot"
    id "io.spring.dependency-management"
}

group = "com.example"
version = "0.0.1-SNAPSHOT"

java {
    sourceCompatibility = "17"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springdoc:springdoc-openapi-starter-webflux-ui:${springDocVersion}")
    implementation("org.springframework.cloud:spring-cloud-starter-gateway")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named("test") {
    useJUnitPlatform()
}

/*AsciiDoctor */

def snippetsDir = file("${buildDir}/generated-snippets")

clean {
    delete("src/main/resources/static")
}

test {
    systemProperty("org.springframework.restdocs.outputDir", snippetsDir)
    outputs.dir(snippetsDir)
}

build {
    dependsOn("asciiDoctorCopyHtml")
}

asciidoctor {
    dependsOn(test)

    inputs.dir(snippetsDir)

    attributes "snippets" : snippetsDir

    doFirst {
        delete("src/main/resources/static")
    }
}

task asciiDoctorCopyHtml(type: Copy){
    dependsOn(asciidoctor)

    from("build/docs/asciidoc/")
    into("src/main/resources/static")
}

bootJar {
    dependsOn(asciidoctor)

    from("build/docs/asiidoc/")
    into("BOOT-INF/classes/static")
}